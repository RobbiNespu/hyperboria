load("@build_bazel_rules_nodejs//:index.bzl", "js_library")
load("@io_bazel_rules_docker//container:container.bzl", "container_push")
load("@io_bazel_rules_docker//nodejs:image.bzl", "nodejs_image")
load("@npm//nuxt:index.bzl", "nuxt")

files = ["nuxt.config.js"] + glob([
    "assets/**",
    "components/**/*.vue",
    "layouts/*.vue",
    "middleware/*.js",
    "pages/**/*.vue",
    "plugins/*.js",
    "static/*",
    "store/*.js",
])

deps = [
    "@npm//@nuxtjs/axios",
    "@npm//bootstrap",
    "@npm//bootstrap-vue",
    "@npm//core-js",
    "@npm//dateformat",
    "@npm//pug",
    "@npm//pug-plain-loader",
    "@npm//sass",
    "@npm//sass-loader",
    "@npm//vue",
    "//nexus/meta_api/js/client",
]

js_library(
    name = "nexus-cognitron-web",
    package_name = "nexus-cognitron-web",
    srcs = files,
    deps = deps,
)

nuxt(
    name = "web-dev",
    args = [
        "-c",
        "nexus/cognitron/web/nuxt.config.js",
        "--watch-poll",
    ],
    data = [":nexus-cognitron-web"],
)

nuxt(
    name = ".nuxt",
    args = [
        "build",
        "--standalone",
        "-c",
        "nexus/cognitron/web/nuxt.config.js",
        "--buildDir=$(@D)",
    ],
    data = files + deps,
    link_workspace_root = True,
    output_dir = True,
)

nodejs_image(
    name = "image",
    base = "//images/production:base-nodejs-image",
    data = glob(["static/*"]) + [
        "nuxt.config.js",
        ":.nuxt",
        "@npm//@nuxtjs/axios",
        "@npm//bootstrap-vue",
        "@npm//nuxt",
    ],
    entry_point = "@npm//:node_modules/nuxt/bin/nuxt.js",
    templated_args = [
        "start",
        "-c",
        "nexus/cognitron/web/nuxt.config.js",
    ],
)


container_push(
    name = "push-public-latest",
    format = "Docker",
    image = ":image",
    registry = "registry.hub.docker.com",
    repository = "thesuperpirate/cognitron-web",
    tag = "latest",
)

container_push(
    name = "push-public-testing",
    format = "Docker",
    image = ":image",
    registry = "registry.hub.docker.com",
    repository = "thesuperpirate/cognitron-web",
    tag = "testing",
)
